buildscript {
    ext {
        springBootVersion = '3.3.4'
        springVersion = '6.1.12'
        tomcatVersion = '10.1.31'
        seleniumVersion = '4.25.0'
        jacksonVersion = '2.18.0'
        nettyVersion = '4.1.114.Final'
        junitJupiterVersion = '5.11.2'
        slf4jVersion = '2.0.16'
        logbackVersion = '1.5.11'
        graalvmVersion = '24.1.1'
    }

    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.owasp:dependency-check-gradle:10.0.3'
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.30.0'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

//
//\
///\
////\
def jlineup_version = "4.13.2-SNAPSHOT"
/////
////
///
//

project.ext.set("debugUpload", false)

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    //maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

apply plugin: 'java'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'org.owasp.dependencycheck'

nexusStaging {
    username = sonatypeUsername
    password = sonatypePassword
    packageGroup = 'de.otto'
}

dependencyCheck {
    nvd {
        apiKey=System.getenv("NVD_API_KEY")
    }
    failBuildOnCVSS = 4
    suppressionFiles = ["${rootDir}/dependency-check-suppressions.xml"]
}

subprojects {
    version = jlineup_version
    group = 'de.otto'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    task allDeps(type: DependencyReportTask) {}

    apply plugin: 'application'
    apply plugin: 'maven-publish'

    apply plugin: 'project-report'
    apply plugin: 'signing'
    apply plugin: 'idea'

    jar {
        manifest.attributes provider: 'gradle'
    }

    test {
        useJUnitPlatform()

        // To allow monitoring System.Exit() we need to allow security manager so it can be overridden in tests
        systemProperty("java.security.manager", "allow")
    }

}

static String runCommand(command) {
    Process proc = command.execute()
    def out = new StringBuffer()
    proc.consumeProcessOutputStream(out)
    proc.consumeProcessErrorStream(out)
    proc.waitFor()
    def errorlevel = proc.exitValue()
    if (errorlevel != 0) {
        throw new RuntimeException("exec failed on command: '${command}' with errorlevel ${errorlevel}".toString())
    }
    //System.err.println("Git Hash: " + out.toString().trim())
    out.toString().trim()
}
