buildscript {
    ext {
        springBootVersion = '3.2.1'
        springVersion = '6.0.14'
        tomcatVersion = '10.1.17'
        seleniumVersion = '4.16.1'
        jacksonVersion = '2.16.0'
        nettyVersion = '4.1.104.Final'
        junitJupiterVersion = '5.10.1'
        slf4jVersion = '2.0.9'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.owasp:dependency-check-gradle:9.0.7'
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.30.0'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

//
//\
///\
////\
def jlineup_version = "4.9.3-SNAPSHOT"
/////
////
///
//

project.ext.set("debugUpload", false)

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    //maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

apply plugin: 'java'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'org.owasp.dependencycheck'

nexusStaging {
    username = sonatypeUsername
    password = sonatypePassword
    packageGroup = 'de.otto'
}

dependencyCheck {
    nvd {
        apiKey=System.getenv("NVD_API_KEY")
    }
    failBuildOnCVSS = 4
    suppressionFiles = ["${rootDir}/dependency-check-suppressions.xml"]
}

subprojects {
    version = jlineup_version
    group = 'de.otto'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    task allDeps(type: DependencyReportTask) {}

    apply plugin: 'application'
    apply plugin: 'maven-publish'

    apply plugin: 'project-report'
    apply plugin: 'signing'
    apply plugin: 'idea'

    jar {
        manifest.attributes provider: 'gradle'
    }

    test {
        useJUnitPlatform()
    }
}

// This function converts 1.8 -> 8
static String compat(String src) {
    if (src.contains('.')) {
        src.substring(src.lastIndexOf('.')+1)
    } else {
        src
    }
}

static String runCommand(command) {
    Process proc = command.execute()
    def out = new StringBuffer()
    proc.consumeProcessOutputStream(out)
    proc.consumeProcessErrorStream(out)
    proc.waitFor()
    def errorlevel = proc.exitValue()
    if (errorlevel != 0) {
        throw new RuntimeException("exec failed on command: '${command}' with errorlevel ${errorlevel}".toString())
    }
    //System.err.println("Git Hash: " + out.toString().trim())
    out.toString().trim()
}
