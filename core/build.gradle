plugins {
    id 'java-library'
}

dependencies {
    implementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}") {
        exclude module: "selenium-edge-driver"
        exclude module: "selenium-ie-driver"
        exclude module: "selenium-opera-driver"
        exclude module: "htmlunit"
    }

    api "com.google.guava:guava:32.1.2-jre"
    api 'com.google.code.gson:gson:2.10.1'

    //Update netty (selenium has older version)
    implementation "io.netty:netty-codec:${nettyVersion}"
    implementation "io.netty:netty-codec-http:${nettyVersion}"
    implementation "io.netty:netty-codec-http2:${nettyVersion}"
    implementation "io.netty:netty-handler:${nettyVersion}"
    implementation "io.netty:netty-codec-socks:${nettyVersion}"
    implementation "io.netty:netty-handler-proxy:${nettyVersion}"
    implementation "io.netty:netty-transport-native-epoll:${nettyVersion}"
    implementation "io.netty:netty-transport-native-kqueue:${nettyVersion}"

    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'

    api "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    api "ch.qos.logback:logback-classic:1.4.11"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.codehaus.janino:janino:3.1.10"
    implementation 'org.thymeleaf:thymeleaf:3.1.2.RELEASE'
    implementation "org.graalvm.sdk:graal-sdk:23.1.0"

    testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
    testCompileOnly 'junit:junit:4.13.2'
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitJupiterVersion}"
    testImplementation "org.hamcrest:hamcrest-core:2.2"
    testImplementation "org.hamcrest:hamcrest-library:2.2"
    testImplementation "org.mockito:mockito-core:5.5.0"
    testImplementation("com.github.stefanbirkner:system-rules:1.19.0") {
        exclude group: 'junit', module: 'junit-dep'
    }

    testImplementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    testImplementation("org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}")
    testImplementation("org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}")
    testImplementation("org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
//        if (details.requested.group == 'com.fasterxml.jackson.core' && details.requested.name == 'jackson-databind') {
//            details.useVersion "2.13.3"
//            details.because 'Enforce databind version'
//        }
    }
}

application {
    mainClass.set("de.otto.jlineup.core.Main")
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    maxHeapSize = "1024m"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

tasks.processResources.doLast { task ->
    def resourcesDir = project.sourceSets.main.output.resourcesDir
    resourcesDir.mkdirs()

    def versionProperties = new File(resourcesDir, "version.properties")
    if (versionProperties) {
        def gitVersion = runCommand("git log -n1 --format=format:%H")
        def fullVersion = version
        versionProperties.text =
                "\njlineup.commit = ${gitVersion}\n" +
                        "\njlineup.version = ${fullVersion}\n" +
                        "\nedison.status.vcs.commit = ${gitVersion}\n" +
                        "\nedison.status.vcs.version = ${fullVersion}\n"

    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jlineup-core'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JLineup Core'
                description = 'The core module of JLineup'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'MediaMarco'
                        name = 'Marco Geweke'
                        email = 'marco.geweke@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/jlineup.git'
                    developerConnection = 'scm:git@github.com:otto-de/jlineup.git'
                    url = 'https://github.com/otto-de/jlineup'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

targetCompatibility = JavaVersion.VERSION_17
sourceCompatibility = JavaVersion.VERSION_17

project.afterEvaluate {
    tasks.withType(JavaCompile) {
        def version = compat(sourceCompatibility)
        project.logger.info("Configuring $name to use --release $version")
        println("Configuring $name to use --release $version")
        options.compilerArgs.addAll(['--release', version])
    }
}
