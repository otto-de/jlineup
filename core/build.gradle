plugins {
    id 'java-library'
}

dependencies {
    implementation("org.seleniumhq.selenium:selenium-java:${seleniumVersion}") {
        exclude module: "selenium-edge-driver"
        exclude module: "selenium-ie-driver"
        exclude module: "selenium-opera-driver"
        exclude module: "htmlunit"
    }

    api "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    api "ch.qos.logback:logback-classic:${logbackVersion}"
    api "com.google.guava:guava:33.4.8-jre"
    api 'com.google.code.gson:gson:2.13.0'

    //Update netty (selenium has older version)
    implementation "io.netty:netty-codec:${nettyVersion}"
    implementation "io.netty:netty-codec-http:${nettyVersion}"
    implementation "io.netty:netty-codec-http2:${nettyVersion}"
    implementation "io.netty:netty-handler:${nettyVersion}"
    implementation "io.netty:netty-codec-socks:${nettyVersion}"
    implementation "io.netty:netty-handler-proxy:${nettyVersion}"
    implementation "io.netty:netty-transport-native-epoll:${nettyVersion}"
    implementation "io.netty:netty-transport-native-kqueue:${nettyVersion}"

    implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.3'

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.codehaus.janino:janino:3.1.12"
    implementation 'org.thymeleaf:thymeleaf:3.1.3.RELEASE'
    implementation "org.graalvm.sdk:graal-sdk:${graalvmVersion}"
    implementation "org.reflections:reflections:0.10.2"

    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitPlatformLauncherVersion}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitJupiterVersion}"
    testCompileOnly "junit:junit:${junitVintageVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
    testImplementation "org.hamcrest:hamcrest:3.0"
    testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"
    testImplementation("com.github.stefanbirkner:system-rules:1.19.0") {
        exclude group: 'junit', module: 'junit-dep'
    }

    testImplementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    testImplementation("org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}")
    testImplementation("org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}")
    testImplementation("org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
//        if (details.requested.group == 'com.fasterxml.jackson.core' && details.requested.name == 'jackson-databind') {
//            details.useVersion "2.13.3"
//            details.because 'Enforce databind version'
//        }
    }
}

application {
    mainClass.set("de.otto.jlineup.core.Main")
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    maxHeapSize = "1024m"
}

tasks.processResources.doLast { task ->
    def resourcesDir = project.sourceSets.main.output.resourcesDir
    resourcesDir.mkdirs()

    def versionProperties = new File(resourcesDir, "version.properties")
    if (versionProperties) {
        def gitVersion = runCommand("git log -n1 --format=format:%H")
        def fullVersion = version
        versionProperties.text =
                "\njlineup.commit = ${gitVersion}\n" +
                        "\njlineup.version = ${fullVersion}\n" +
                        "\nedison.status.vcs.commit = ${gitVersion}\n" +
                        "\nedison.status.vcs.version = ${fullVersion}\n"

    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "de.otto"
            artifactId = 'jlineup-core'
            from components.java
            pom {
                name = 'JLineup Core'
                description = 'The core module of JLineup'
            }
        }
    }
}
