def seleniumVersion = '3.141.59'
def jacksonVersion = '2.12.1'
ext['selenium.version'] = seleniumVersion
ext['jackson.version'] = jacksonVersion

dependencies {
    compile("org.seleniumhq.selenium:selenium-java:" + seleniumVersion) {
        exclude module: "selenium-edge-driver"
        exclude module: "selenium-ie-driver"
        exclude module: "selenium-opera-driver"
        exclude module: "selenium-safari-driver"
        exclude module: "htmlunit"
    }

    //Update guava because selenium brings version 25 with CVEs
    compile "com.google.guava:guava:30.1.1-jre"
    //Update okhttp because selenium brings older one with 4.9.0
    compile "com.squareup.okhttp3:okhttp:4.9.1"
    //Update kotlin because okhttp 4.9.0 brings 1.4.10
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.5.10"

    compile "io.github.bonigarcia:webdrivermanager:4.4.3"

    compile "com.codeborne:phantomjsdriver:1.4.4"
    //Update legacy apache http client that is introduced by phantomjsdriver
    compile "org.apache.httpcomponents:httpclient:4.5.13"
    //Update commons-codec from 1.11 which has a vulnerability
    compile "commons-codec:commons-codec:1.15"

    compile "com.fasterxml.jackson.core:jackson-core:" + jacksonVersion
    compile "com.fasterxml.jackson.core:jackson-annotations:" + jacksonVersion
    compile "com.fasterxml.jackson.core:jackson-databind:" + jacksonVersion
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:" + jacksonVersion
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "org.slf4j:slf4j-api:1.7.31"
    compile "org.codehaus.janino:janino:3.1.4"
    compile "org.thymeleaf:thymeleaf:3.0.12.RELEASE"
    compile "org.graalvm.sdk:graal-sdk:21.1.0"
    testCompile "org.hamcrest:hamcrest-core:2.2"
    testCompile "org.hamcrest:hamcrest-library:2.2"
    testCompile "junit:junit:4.13.2"

    testCompile "org.mockito:mockito-core:3.11.1"
    testCompile("com.github.stefanbirkner:system-rules:1.19.0") {
        exclude group: 'junit', module: 'junit-dep'
    }

    testCompile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
}

application {
    mainClass.set("de.otto.jlineup.core.Main")
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

tasks.processResources.doLast { task ->
    def resourcesDir = project.sourceSets.main.output.resourcesDir
    resourcesDir.mkdirs()

    def versionProperties = new File(resourcesDir, "version.properties")
    if (versionProperties) {
        def gitVersion = runCommand("git log -n1 --format=format:%H")
        def fullVersion = version
        versionProperties.text =
                "\njlineup.commit = ${gitVersion}\n" +
                        "\njlineup.version = ${fullVersion}\n" +
                        "\nedison.status.vcs.commit = ${gitVersion}\n" +
                        "\nedison.status.vcs.version = ${fullVersion}\n"

    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jlineup-core'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JLineup Core'
                description = 'The core module of JLineup'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'MediaMarco'
                        name = 'Marco Geweke'
                        email = 'marco.geweke@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/jlineup.git'
                    developerConnection = 'scm:git@github.com:otto-de/jlineup.git'
                    url = 'https://github.com/otto-de/jlineup'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

project.afterEvaluate {
    tasks.withType(JavaCompile) {
        def version = compat(sourceCompatibility)
        project.logger.info("Configuring $name to use --release $version")
        options.compilerArgs.addAll(['--release', version])
    }
}
