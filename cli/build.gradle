def seleniumVersion = '3.141.59'
ext['selenium.version'] = seleniumVersion

apply plugin: 'org.springframework.boot'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(":jlineup-core")
    compile "info.picocli:picocli:4.0.4"
    annotationProcessor 'info.picocli:picocli-codegen:4.0.4'
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "com.oracle.substratevm:svm:19.2.0.1"

    testCompile project(path: ":jlineup-core", configuration: 'testCompile')
}

tasks.distZip.enabled = false
tasks.distTar.enabled = false

tasks.bootDistZip {
    baseName = project.archivesBaseName
}

tasks.bootDistTar {
    baseName = project.archivesBaseName
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

jar {
    enabled = false
}

application {
    mainClassName = 'de.otto.jlineup.cli.Main'
}

bootJar {
    enabled = true
    launchScript()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jlineup-cli'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JLineup CLI'
                description = 'The cli version of JLineup'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'MediaMarco'
                        name = 'Marco Geweke'
                        email = 'marco.geweke@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/jlineup.git'
                    developerConnection = 'scm:git@github.com:otto-de/jlineup.git'
                    url = 'https://github.com/otto-de/jlineup'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

static String runCommand(command) {
    Process proc = command.execute()
    def out = new StringBuffer()
    proc.consumeProcessOutputStream(out)
    proc.consumeProcessErrorStream(out)
    proc.waitFor()
    def errorlevel = proc.exitValue()
    if (errorlevel != 0) {
        throw new RuntimeException("exec failed on command: '${command}' with errorlevel ${errorlevel}".toString())
    }
    //System.err.println("Git Hash: " + out.toString().trim())
    out.toString().trim()
}
