def seleniumVersion = '3.8.1'
ext['selenium.version'] = seleniumVersion

apply plugin: 'org.springframework.boot'

dependencies {
    compile project(":core")
    compile "com.beust:jcommander:1.72"
    compile "ch.qos.logback:logback-classic:1.2.3"

    testCompile project(path: ":core", configuration: 'testCompile')
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName = 'jlineup-cli'
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    baseName = 'jlineup-cli'
    from sourceSets.main.allSource
    classifier = 'sources'
}

jar {
    baseName = "jlineup"
    archiveName = "jlineup.jar"
}

bootJar {
    enabled = true
    baseName = "jlineup"
    archiveName = "jlineup.jar"
    mainClassName = "de.otto.jlineup.cli.Main"
    launchScript()
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives bootJar
}

[distZip, bootDistZip, distTar, bootDistTar].each { task ->
    configurations.archives.artifacts.removeAll
            { it.class.simpleName == "ArchivePublishArtifact" && it.archiveTask == task }
    task.enabled = false
}

signing {
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.debugUpload) {
                // use this for local upload debugging instead of sonatype
                repository(url: "file://${buildDir}/repo")
                snapshotRepository(url: "file://${buildDir}/repo")
            } else {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            pom.artifactId = "jlineup-cli"
            pom.project {
                name 'JLineup Command Line Interface'
                packaging 'jar'
                description 'Webapp image comparison tool'
                url 'http://github.com/otto-de/jlineup'

                scm {
                    url 'scm:git@github.com:otto-de/jlineup.git'
                    connection 'scm:git@github.com:otto-de/jlineup.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mgeweke'
                        name 'Marco Geweke'
                        url 'https://github.com/MediaMarco'
                    }
                }
            }
        }
    }
}

static String runCommand(command) {
    Process proc = command.execute()
    def out = new StringBuffer()
    proc.consumeProcessOutputStream(out)
    proc.consumeProcessErrorStream(out)
    proc.waitFor()
    def errorlevel = proc.exitValue()
    if (errorlevel != 0) {
        throw new RuntimeException("exec failed on command: '${command}' with errorlevel ${errorlevel}".toString())
    }
    out.toString().trim()
}
