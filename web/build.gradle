import org.springframework.boot.gradle.tasks.bundling.BootJar

apply plugin: 'org.springframework.boot'

dependencies {
    implementation project(":jlineup-core")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    implementation 'org.yaml:snakeyaml:2.4'
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}")
    implementation("de.otto.edison:edison-core:3.4.4")

    //Update hibernate validator that comes with edison-core
    implementation("org.hibernate.validator:hibernate-validator:9.0.0.Final")
    //Update commons-codec
    implementation("commons-codec:commons-codec:1.18.0")
    //Update embedded tomcat
    implementation("org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}")
    implementation("org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}")
    implementation("org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}")

    testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
    testCompileOnly 'junit:junit:4.13.2'
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitPlatformLauncherVersion}"
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testImplementation('io.rest-assured:rest-assured:5.5.2')
    testImplementation("org.awaitility:awaitility:4.3.0")

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.springframework.boot') {
            details.useVersion "${springBootVersion}"
            details.because 'Enforce spring boot version'
        }
        else if (details.requested.group == 'org.springframework') {
            details.useVersion "${springVersion}"
            details.because 'Enforce spring boot version'
        }
    }
}

configurations.all {
    exclude group: 'commons-logging', module: 'commons-logging'
}

application {
    mainClass.set("de.otto.jlineup.web.JLineupWebApplication")
}

bootJar {
    mainClass = "de.otto.jlineup.web.JLineupWebApplication"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "de.otto"
            artifactId = 'jlineup-web'
            from components.java
            pom {
                name = 'JLineup Web'
                description = 'The web variant of JLineup'
            }
        }
    }
}