apply plugin: 'org.springframework.boot'

dependencies {
    implementation project(":jlineup-core")
    implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}")
    implementation("de.otto.edison:edison-core:2.4.1") {
        exclude group: 'software.amazon.awssdk', module: '*'
    }
    //Update hibernate validator that comes with edison-core
    implementation("org.hibernate.validator:hibernate-validator:6.0.22.Final")
    //Update commons-codec
    implementation("commons-codec:commons-codec:1.15")
    //Update embedded tomcat
    implementation("org.apache.tomcat.embed:tomcat-embed-core:9.0.41")

    compileOnly("com.beust:jcommander:1.78")
    compileOnly("io.netty:netty-handler:4.1.58.Final")

    testCompile project(path: ":jlineup-core", configuration: 'testCompile')
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testCompile("io.rest-assured:rest-assured:4.3.1")
    testCompile("org.awaitility:awaitility:4.0.3")

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
}

tasks.distZip.enabled = false
tasks.distTar.enabled = false

tasks.bootDistZip {
    baseName = project.archivesBaseName
}

tasks.bootDistTar {
    baseName = project.archivesBaseName
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

jar {
    enabled = false
}

application {
    mainClassName = "de.otto.jlineup.web.JLineupWebApplication"
}

bootJar {
    enabled = true
    mainClassName = "de.otto.jlineup.web.JLineupWebApplication"
    launchScript()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jlineup-web'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JLineup Web'
                description = 'The web variant of JLineup'
                url = 'https://github.com/otto-de/jlineup'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'MediaMarco'
                        name = 'Marco Geweke'
                        email = 'marco.geweke@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:otto-de/jlineup.git'
                    developerConnection = 'scm:git@github.com:otto-de/jlineup.git'
                    url = 'https://github.com/otto-de/jlineup'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            if (project.debugUpload) {
                releasesRepoUrl = "$buildDir/repos/releases"
                snapshotsRepoUrl = "$buildDir/repos/snapshots"
            } else {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}